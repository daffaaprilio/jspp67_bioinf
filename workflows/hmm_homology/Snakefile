# workflows/hmm_homology/Snakefile

configfile: "../../config.yaml"

SEEDS           = config['seeds']       # {name: fasta_path}
DBS             = config['dbs']         # {name: blast_db_string}
TARGET_FASTA    = config['target_fasta']
RESULTS_DIR     = config['results_homology_dir']

'''
A sensitive homology detection strategy:
1. BLAST finds close homologs (remote search against NCBI nr/refseq, Viridiplantae only).
2. HMM captures evolutionary patterns from these homologs and finds conserved
   regions in the Sorghum proteome.
3. HMM hit protein accessions are converted to Sorghum gene IDs via gene2accession.
4. Converts gene ID into Entrez Gene ID
Each (seed x db) combination runs independently in its own subdirectory.
'''


rule all:
    input:
        expand(
            [
                f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl",
                f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-07-homologous_geneID.txt"
            ],
            seed=SEEDS.keys(),
            db=DBS.keys(),
        )


rule blast_seed_gene:
    input:
        fasta = lambda wc: SEEDS[wc.seed]
    params:
        db              = lambda wc: DBS[wc.db],
        entrez_query    = "txid{taxids}[Organism]".format(taxids=config['taxids']),
        max_target_seqs = config['max_target_seqs'],
        outfmt          = "6 qacc sacc pident length qcovs evalue bitscore staxids"
    output:
        archive = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-00-blast_archive.txt",
        raw     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-00-raw_blast_hits.txt",
        tab     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-01-blast_hits.txt"
    shell:
        """
        # run archive blast
        blastp \
            -remote \
            -db {params.db} \
            -query {input.fasta} \
            -entrez_query "{params.entrez_query}" \
            -max_target_seqs {params.max_target_seqs} \
            -outfmt 11 > {output.archive}

        # parse tabular and human-readable blast output
        blast_formatter \
            -archive {output.archive} \
            -outfmt "{params.outfmt}" > {output.tab}

        blast_formatter \
            -archive {output.archive} \
            -outfmt 0 > {output.raw}
        """


rule obtain_accessions:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-01-blast_hits.txt"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-02-accessions.txt"
    shell:
        """
        cut -f2 {input} | sort -u > {output}
        """


rule download_sequences:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-02-accessions.txt"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-03.faa"
    shell:
        """
        while read acc; do
            if [ -n "$acc" ]; then
                efetch -db protein -id "$acc" -format fasta >> {output}
                sleep 0.1
            fi
        done < {input}
        """


rule align_filtered_sequences:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-03.faa"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-04.aln.faa"
    shell:
        """
        mafft --auto {input} > {output}
        """


rule build_hmm_profile:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-04.aln.faa"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm"
    shell:
        """
        hmmbuild {output} {input}
        """


rule hmmpress:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm"
    output:
        multiext(
            f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
            ".h3f", ".h3i", ".h3m", ".h3p"
        )
    shell:
        """
        hmmpress {input}
        """


rule hmmsearch:
    input:
        hmm     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
        target  = TARGET_FASTA,
        pressed = multiext(
            f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
            ".h3f", ".h3i", ".h3m", ".h3p"
        )
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl"
    shell:
        """
        hmmsearch \
            --max \
            --tblout {output} \
            {input.hmm} {input.target}
        """

rule obtain_gene2accession:
    params:
        reference_dir   = '../../data/reference'
    output: 
        '../../data/reference/sorghum_gene2accession'
    shell:
        '''
        # create directory
        if [[ ! -e {params.reference_dir} ]]; then
            mkdir -p {params.reference_dir}
        fi
        # download from NCBI FTP (taxdump: gene2accession)
        wget -q -P {params.reference_dir} https://ftp.ncbi.nlm.nih.gov/gene/DATA/gene2accession.gz
        gunzip -f {params.reference_dir}/gene2accession.gz

        # filter to only include sorghum conversion (taxid 4558)
        awk -F'\t' '$1 == 4558' {params.reference_dir}/gene2accession > {output}
        '''

rule convert_id:
    input:
        script          = '../hmm_homology/scripts/convert_id.py',
        gene2accession  = '../../data/reference/sorghum_gene2accession',
        hmm_result      = f'{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl'
    output:
        f'{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-07-homologous_geneID.txt'
    shell:
        '''
        python {input.script} -m {input.hmm_result} -r {input.gene2accession} -o {output}
        '''