# workflows/hmm_homology/Snakefile

## next update: streamline, cut down unnecessary steps (download accession, etc.)

# configfile: "/Users/daffaaprilio/Documents/Work/jspp67_bioinf/configs/config.yaml"

WDIR            = config['wdir']
SEEDS           = {k: f"{WDIR}/{v}" for k, v in config['seeds'].items()}
DBS             = config['dbs']         # {name: blast_db_string}
TARGET_FASTA    = f"{WDIR}/{config['target_fasta']}"
RESULTS_DIR     = f"{WDIR}/{config['results_homology_dir']}"
BLASTDB_DIR     = f"{WDIR}/{config['blastdb_dir']}"

# Optional manual sequences: skip the BLAST pipeline by providing pre-downloaded FASTAs.
# Keys must be "{seed}_{db}"; values are paths (relative paths resolved against WDIR).
MANUAL_SEQS = {
    k: (v if v.startswith("/") else f"{WDIR}/{v}")
    for k, v in config.get('manual_seqs', {}).items()
}

'''
A sensitive homology detection strategy:
1. BLAST finds close homologs (remote search against NCBI nr/refseq, Viridiplantae only).
2. HMM captures evolutionary patterns from these homologs and finds conserved
   regions in the Sorghum proteome.
3. HMM hit protein accessions are converted to Sorghum gene IDs via gene2accession.
4. Converts gene ID into Entrez Gene ID
Each (seed x db) combination runs independently in its own subdirectory.
'''


rule all:
    input:
        expand(
            [
                f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl",
                f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-07-homologous_geneID.txt"
            ],
            seed=SEEDS.keys(),
            db=DBS.keys(),
        )

rule blast_seed_gene:
    input:
        fasta  = lambda wc: SEEDS[wc.seed],
    params:
        db              = lambda wc: DBS[wc.db],
        blastdb_dir     = BLASTDB_DIR,
        max_target_seqs = config['max_target_seqs'],
        outfmt          = "6 qacc sacc pident length qcovs evalue bitscore staxids",
    output:
        archive = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-00-blast_archive.txt",
        raw     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-00-raw_blast_hits.txt",
        tab     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-01-blast_hits.txt"
    retries: 2
    shell:
        """
        set -euo pipefail
        export BLASTDB={params.blastdb_dir}

        # run archive blast against local database
        blastp \
            -db {params.db} \
            -query {input.fasta} \

            -max_target_seqs {params.max_target_seqs} \
            -outfmt 11 > {output.archive}

        # parse tabular and human-readable blast output
        blast_formatter \
            -archive {output.archive} \
            -outfmt "{params.outfmt}" > {output.tab}

        blast_formatter \
            -archive {output.archive} \
            -outfmt 0 > {output.raw}

        if [ ! -s {output.tab} ]; then
            echo "ERROR: BLAST returned no hits for {wildcards.seed} vs {wildcards.db}." >&2
            exit 1
        fi
        """


# When manual_seqs is configured for a seed_db combination, this rule
# copies the provided FASTA directly to *-03.faa, bypassing the BLAST pipeline.
rule use_manual_fasta:
    input:
        fasta = lambda wc: MANUAL_SEQS[f"{wc.seed}_{wc.db}"]
    output:
        f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-03.faa"
    shell:
        "cp {input.fasta} {output}"

# Prefer manual sequences over the BLAST-derived download when both rules apply.
ruleorder: use_manual_fasta > download_sequences


rule obtain_accessions:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-01-blast_hits.txt"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-02-accessions.txt"
    shell:
        """
        cut -f2 {input} | sort -u > {output}
        """


rule obtain_sequences:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-02-accessions.txt"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-03.faa"
    shell:
        """
        blastdbcmd \
            -db {DBS[wc.db]} \
            -entry_batch {input} \
            -out {output}
        """


rule align_filtered_sequences:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-03.faa"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-04.aln.faa"
    shell:
        """
        mafft --auto {input} > {output}
        """


rule build_hmm_profile:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-04.aln.faa"
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm"
    shell:
        """
        hmmbuild {output} {input}
        """


rule hmmpress:
    input:  f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm"
    output:
        multiext(
            f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
            ".h3f", ".h3i", ".h3m", ".h3p"
        )
    shell:
        """
        hmmpress {input}
        """


rule hmmsearch:
    input:
        hmm     = f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
        target  = TARGET_FASTA,
        pressed = multiext(
            f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-05.hmm",
            ".h3f", ".h3i", ".h3m", ".h3p"
        )
    output: f"{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl"
    shell:
        """
        hmmsearch \
            --max \
            --tblout {output} \
            {input.hmm} {input.target}
        """

rule obtain_gene2accession:
    params:
        reference_dir   = f"{WDIR}/data/reference"
    output: 
        f"{WDIR}/data/reference/sorghum_gene2accession"
    shell:
        '''
        # create directory
        if [[ ! -e {params.reference_dir} ]]; then
            mkdir -p {params.reference_dir}
        fi
        # download from NCBI FTP (taxdump: gene2accession)
        wget -q -P {params.reference_dir} https://ftp.ncbi.nlm.nih.gov/gene/DATA/gene2accession.gz
        gunzip -f {params.reference_dir}/gene2accession.gz

        # filter to only include sorghum conversion (taxid 4558)
        awk -F'\t' '$1 == 4558' {params.reference_dir}/gene2accession > {output}
        '''

rule convert_id:
    input:
        script          = f'{WDIR}/workflows/hmm_homology/scripts/convert_id.py',
        gene2accession  = f'{WDIR}/data/reference/sorghum_gene2accession',
        hmm_result      = f'{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-06-results.tbl'
    output:
        f'{RESULTS_DIR}/{{seed}}_{{db}}/{{seed}}_{{db}}-07-homologous_geneID.txt'
    shell:
        '''
        python {input.script} -m {input.hmm_result} -r {input.gene2accession} -o {output}
        '''