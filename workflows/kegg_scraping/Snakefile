# workflow/kegg_scraping/Snakefile"

from pathlib import Path

configfile: "/Users/daffaaprilio/Documents/Work/jspp67_bioinf/configs/config.yaml"

WDIR                = config['wdir']
SPECIES             = config['species'] # atted-plants: (sbi zma ... ath)
PATHWAYS            = config['pathways'].split() if isinstance(config['pathways'], str) else list(config['pathways']) # e.g. ["map00020", "map00660"]

TARGET_FASTA        = f"{WDIR}/{config['target_fasta']}"
KEGG_DIR            = f"{WDIR}/{config['reference_kegg_dir']}"
RESULTS_DIR         = f"{WDIR}/{config['results_homology_dir']}"
RESULTS_KEGG_DIR    = f"{WDIR}/{config['results_kegg_dir']}"
RESULTS_KO_DIR      = f"{WDIR}/{config['results_ko_dir']}"

"""
Create a bridging from 
(a) Obtaining kegg_pathway_genes.py script and 
(2) the HMM Homology module (../hmm_homology/Snakefile).

Run this everytime you want to get the list of bait genes, even when you 
only provide one species for the kegg_pathway_genes.py script. 
It is natural that the mentioned python script output more than one gene for 
one reaction. HMM homology will find the best fitting one (based on the result.tbl evalue info).
"""

def get_pathway_gene_files(wildcards):
    """Return per-pathway TSV paths after ensuring checkpoint kegg_pathway_genes has run."""
    # trigger checkpoint dependency; PATHWAYS is already known from config so no glob needed
    checkpoints.kegg_pathway_genes.get(**wildcards)
    return [
        f"{RESULTS_KEGG_DIR}/{SPECIES}_{pathway}_genes.tsv"
        for pathway in PATHWAYS
    ]

def get_separated_by_ko_files(wildcards):
    """Return per-pathway-ko TSV paths after ensuring checkpoint separate_by_function has run."""
    checkpoints.separate_by_function.get(**wildcards)
    return [
        f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-01-pathway_genes_from_KEGG.txt"
    ]


def aggregate_final_outputs(wildcards):
    checkpoint_output = checkpoints.separate_by_function.get(**wildcards).output[0]
    # discover all KO IDs from the directory that the checkpoint created
    ko_ids, = glob_wildcards(Path(checkpoint_output) / "{seed}_kegg" / "{seed}_kegg-01-pathway_genes_from_KEGG.txt")
    
    return expand(
        f"{RESULTS_DIR}/{{seed}}_kegg/{{seed}}_kegg-07-homologous_geneID.txt",
        seed=ko_ids
    )

rule all:
    input:
        aggregate_final_outputs

checkpoint kegg_pathway_genes:
    """Run kegg_pathway_genes.py with all pathways in one call; outputs to RESULTS_KEGG_DIR."""
    input:
        script  = f'{WDIR}/workflows/kegg_scraping/scripts/kegg_pathway_genes.py',
    output:
        directory(RESULTS_KEGG_DIR)
    params:
        kegg_d   = KEGG_DIR,
        pathways = " ".join(PATHWAYS),
        sp       = SPECIES,
    shell:
        """
        python {input.script} \
            --pathways {params.pathways} \
            --species {params.sp} \
            --kegg-dir {params.kegg_d} \
            --output {output}
        """

checkpoint separate_by_function:
    """Split each per-pathway TSV (one per pathway from kegg_pathway_genes) by KO function."""
    input:  
        pathway_gene_files  = get_pathway_gene_files, 
        script              = f'{WDIR}/workflows/kegg_scraping/scripts/separate_pathway_gene_files.py'
    output: 
        directory(RESULTS_KO_DIR)
    run:
        import os
        os.makedirs(output[0], exist_ok=True)
        for f in input.pathway_gene_files:
            # extract pathway ID from filename: {SPECIES}_{pathway}_genes.tsv
            stem    = Path(f).stem  # e.g. "atted-plants_map00020_genes"
            pathway = stem[len(SPECIES) + 1 : -len("_genes")]
            shell(
                f"python {input.script} "
                f"--input {f} "
                f"--output {output[0]} "
                f"--pathway {pathway}"
            )

rule obtain_spgeneid:
    input:
        # tsv    = f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-01-pathway_genes_from_KEGG.txt",
        tsv     = get_separated_by_ko_files,
        script  = f'{WDIR}/workflows/kegg_scraping/scripts/obtain_spgeneid.py'
    output:
        f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-02-spgeneid.txt"
    shell:
        """
        python {input.script} --input {input.tsv} --output {output}
        """

rule obtain_sequences:
    input:
        spgeneid = f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-02-spgeneid.txt",
        script   = f'{WDIR}/workflows/kegg_scraping/scripts/obtain_aa_sequences.py'
    output:
        f"{RESULTS_DIR}/{{seed}}_kegg/{{seed}}_kegg-03.faa"
    params:
        kegg_dir = KEGG_DIR
    shell:
        """
        python {input.script} --input {input.spgeneid} --kegg-dir {params.kegg_dir} --output {output}
        """

# after the rule obtain_sequences, go to HMM homology modules' rule align_filtered sequences

ruleorder: obtain_sequences > hmm_homology_download_sequences

module hmm_homology:
    snakefile: "../hmm_homology/Snakefile"
    config: config

use rule * from hmm_homology as hmm_homology_*

