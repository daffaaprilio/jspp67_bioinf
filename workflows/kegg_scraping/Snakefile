# workflow/kegg_scraping/Snakefile"

from pathlib import Path

configfile: "../../configs/config.yaml"

WDIR                = config['wdir']
SPECIES             = config['species'] # atted-plants: (sbi zma ... ath)
PATHWAY             = config['pathways'] # map00020

TARGET_FASTA        = f"{WDIR}/{config['target_fasta']}"
KEGG_DIR            = f"{WDIR}/{config['reference_kegg_dir']}"
RESULTS_DIR         = f"{WDIR}/{config['results_homology_dir']}"
RESULTS_KEGG_DIR    = f"{WDIR}/{config['results_kegg_dir']}"
RESULTS_KO_DIR      = f"{WDIR}/{config['results_ko_dir']}"

"""
Create a bridging from 
(a) Obtaining kegg_pathway_genes.py script and 
(2) the HMM Homology module (../hmm_homology/Snakefile).

Run this everytime you want to get the list of bait genes, even when you 
only provide one species for the kegg_pathway_genes.py script. 
It is natural that the mentioned python script output more than one gene for 
one reaction. HMM homology will find the best fitting one (based on the result.tbl evalue info).
"""

def aggregate_final_outputs(wildcards):
    checkpoint_output = checkpoints.separate_by_function.get(**wildcards).output[0]
    # discover all KO IDs from the directory that the checkpoint created
    ko_ids, = glob_wildcards(Path(checkpoint_output) / "{seed}_kegg" / "{seed}_kegg-01-pathway_genes_from_KEGG.txt")
    
    return expand(
        f"{RESULTS_DIR}/{{seed}}_kegg/{{seed}}_kegg-07-homologous_geneID.txt",
        seed=ko_ids
    )

rule all:
    input:
        aggregate_final_outputs

rule kegg_pathway_genes:
    input:
        script  = 'scripts/kegg_pathway_genes.py',
    output:
        f"{RESULTS_KEGG_DIR}/{SPECIES}_{PATHWAY}_genes.tsv"
    params:
        kegg_d  = KEGG_DIR,
        pathway = PATHWAY,
        sp      = SPECIES,
    shell:
        """
        python {input.script} \
            --pathway {params.pathway} \
            --species {params.sp} \
            --kegg-dir {params.kegg_d} \
            --output {output}
        """

checkpoint separate_by_function:
    input:  
        pathway_gene_file   = f"{RESULTS_KEGG_DIR}/{SPECIES}_{PATHWAY}_genes.tsv", # pathway gene file (output from the kegg_pathway_genes.py script)
        script              = 'scripts/separate_pathway_gene_files.py'
    output: 
        directory(RESULTS_KO_DIR)
    params:
        pathway     = PATHWAY
    shell:
        """
        python {input.script} --input {input.pathway_gene_file} --output {output} --pathway {params.pathway}
        """

rule obtain_spgeneid:
    input:
        tsv    = f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-01-pathway_genes_from_KEGG.txt",
        script = 'scripts/obtain_spgeneid.py'
    output:
        f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-02-spgeneid.txt"
    shell:
        """
        python {input.script} --input {input.tsv} --output {output}
        """

rule obtain_sequences:
    input:
        spgeneid = f"{RESULTS_KO_DIR}/{{seed}}_kegg/{{seed}}_kegg-02-spgeneid.txt",
        script   = 'scripts/obtain_aa_sequences.py'
    output:
        f"{RESULTS_DIR}/{{seed}}_kegg/{{seed}}_kegg-03.faa"
    params:
        kegg_dir = KEGG_DIR
    shell:
        """
        python {input.script} --input {input.spgeneid} --kegg-dir {params.kegg_dir} --output {output}
        """

# after the rule obtain_sequences, go to HMM homology modules' rule align_filtered sequences

ruleorder: obtain_sequences > hmm_homology_download_sequences

module hmm_homology:
    snakefile: "../hmm_homology/Snakefile"
    config: config

use rule * from hmm_homology as hmm_homology_*

